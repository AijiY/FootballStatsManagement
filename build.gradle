plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	// node.js
	id 'com.github.node-gradle.node' version '5.0.0'
}

group = 'football'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

//	Thymeleaf
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

//	Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

//	OpenAPI Generator
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

//	便利機能追加
	implementation 'org.apache.commons:commons-lang3:3.15.0'
// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

//	MySQLドライバ
	runtimeOnly 'com.mysql:mysql-connector-j'

//	MyBatis
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'

	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

//	テスト
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
	testImplementation 'com.h2database:h2:2.3.232'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Node.js
node {
	version = '20.11.1'
	npmVersion = '10.2.4'
	download = true
}

// Reactアプリをビルドするタスク
task buildReact(type: Exec) {
	workingDir "$projectDir/react-app"
	commandLine 'npm.cmd', 'run', 'build'
}

// static フォルダを削除するタスク
task deleteStatic(type: Delete) {
	dependsOn buildReact // Reactのビルドが終わってからコピーを実行
	delete "$projectDir/src/main/resources/static"
}

// Reactのビルド結果をSpring Bootのstaticフォルダにコピーするタスク
task copyToStatic(type: Copy) {
	dependsOn deleteStatic // staticフォルダを削除してからコピーを実行
	from "$projectDir/react-app/build"
	into "$projectDir/src/main/resources/static"
}

// パッケージング時にもReactのビルドとコピーが行われるように設定
processResources.dependsOn copyToStatic

gradle.taskGraph.whenReady { taskGraph ->
	if (taskGraph.hasTask(':test')) {
		// テスト実行時にReact関連タスクを無効にする
		tasks.buildReact.enabled = false
		tasks.deleteStatic.enabled = false
		tasks.copyToStatic.enabled = false
	}
}


